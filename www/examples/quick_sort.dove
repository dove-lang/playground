// Dove 0.1.2 (default)
// Visit https://github.com/dove-lang for more information.


/*
    This function takes last element as pivot, places the pivot element at its correct 
    position in sorted array, and places all smaller (smaller than pivot) to left of 
    pivot and all greater elements to right of pivot. 
*/
fun partition(arr, low, high) {
    let i = low - 1             // Index of smaller element
    let pivot = arr[high]       // Pivot

    for j in low..high {
        // If current element is smaller than or equal to the pivot
        if arr[j] <= pivot {
            // Increment the index of smaller element
            i ++
            let temp_j = arr[j]
            arr[j] = arr[i]
            arr[i] = temp_j
        }
    }

    let temp_h = arr[high]
    arr[high] = arr[i+1]
    arr[i+1] = temp_h
    return i + 1
}

fun quick_sort(arr, low, high) {
    if low < high {
        let partition_i = partition(arr, low, high)
        quick_sort(arr, low, partition_i-1)
        quick_sort(arr, partition_i+1, high)
    }
}

let arr = [3, 2, 7, 1, 4, -2]
let n = arr.len()
quick_sort(arr, 0, n-1)

print(arr)
